#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	УстановитьДоступностьКПП(Объект.Тип, Элементы.КПП);
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Не РаботаСИННКлиентСервер.ИННВерен(Объект.ИНН, Объект.Тип) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	УстановитьДоступностьКПП(Объект.Тип, Элементы.КПП);
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	ТекстОшибки = "";
	Если Не РаботаСИННКлиентСервер.ИННВерен(Объект.ИНН, Объект.Тип, ТекстОшибки) Тогда
		Элементы.ИннДекорация.Видимость = Истина;
		Элементы.ИннДекорация.Заголовок = ТекстОшибки;
	Иначе
		Элементы.ИннДекорация.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Объект.ПолноеНаименование = ПолучитьПолноеНаименование(Объект.Наименование);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКПП(ТипКонтрагента, КПП)
	
	Если ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо") Тогда
		КПП.Видимость = Истина;
	Иначе
		КПП.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПолноеНаименование(СокращенноеНаименование)
	
	Сокращения = Новый Структура;
	Сокращения.Вставить("АО", "Акционерное общество");
	Сокращения.Вставить("ООО", "Общество с ограниченной ответственностью");
	Сокращения.Вставить("ЗАО", "Закрытое акционерное общество");
	Сокращения.Вставить("ИП", "Индивидуальный предприниматель");
	
	Для Каждого Сокращение Из Сокращения Цикл
		Если СтрНачинаетсяС(СокращенноеНаименование, Сокращение.Ключ) Тогда
			Возврат Сокращение.Значение + Сред(СокращенноеНаименование, СтрДлина(Сокращение.Ключ) + 1);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СокращенноеНаименование;
	
КонецФункции

#КонецОбласти






















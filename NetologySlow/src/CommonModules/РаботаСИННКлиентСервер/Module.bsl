#Область СлужебныеПроцедурыИФункции

Функция ИННВерен(ИНН, ТипКонтрагента, ТекстОшибки = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		Возврат Истина;
	КонецЕсли;
	
	КорректнаяДлинаИНН = КорректнаДлинаИНН(ТипКонтрагента);
	Если СтрДлина(ИНН) <> КорректнаяДлинаИНН Тогда
		ТекстОшибки = "ИНН некорректной длины";
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ТолькоЦифры(ИНН) Тогда
		ТекстОшибки = "В ИНН введены некорректные символы";
		Возврат Ложь;;
	КонецЕсли;
	
	Если НЕ КонтрольИННВерен(ИНН) Тогда
		ТекстОшибки = "Не совпадают контрольные числа ИНН";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
		
КонецФункции

Функция ТолькоЦифры(ИНН)
	Цифры = "0123456789";
	Для Сч = 1 По СтрДлина(ИНН) Цикл
		Символ = Сред(ИНН, Сч, 1);
		Если СтрНайти(Цифры, Символ) = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

Функция КорректнаДлинаИНН(ТипКонтрагента)
	Если ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо") Тогда
		КорректнаяДлинаИНН =  12;
	Иначе
		КорректнаяДлинаИНН =  10;
	КонецЕсли;
	Возврат КорректнаяДлинаИНН
КонецФункции

Функция КонтрольИННВерен(ИНН)
	
	Если СтрДлина(ИНН) = 10 Тогда
		
		КонтрольнаяСумма = ВернутьКонтрольнуюСумму(ИНН, "2,4,10,3,5,9,4,6,8");
		Возврат ?(КонтрольнаяСумма % 11 % 10 = Число(Прав(ИНН, 1)), Истина, Ложь);
	
	Иначе
		
		КонтрольнаяСумма1 = ВернутьКонтрольнуюСумму(ИНН, "7,2,4,10,3,5,9,4,6,8");
		КонтрольноеЧисло1 = КонтрольнаяСумма1 % 11 % 10;
		
		КонтрольнаяСумма2 = ВернутьКонтрольнуюСумму(ИНН, "3,7,2,4,10,3,5,9,4,6,8");
		КонтрольноеЧисло2 = КонтрольнаяСумма2 % 11 % 10;
		Возврат ?(КонтрольноеЧисло1 = Число(Сред(ИНН, 11, 1)) И КонтрольноеЧисло2 = Число(Прав(ИНН, 1)), Истина, Ложь);
			
	КонецЕсли;
	
КонецФункции

Функция ВернутьКонтрольнуюСумму(ИНН, СтрМножители)
	
	КонтрольнаяСумма = 0;
	Множители = СтрРазделить(СтрМножители, ",", Ложь);
	Для Сч = 1 По Множители.Количество() Цикл
		ЦифраИНН = Число(Сред(ИНН, Сч, 1));
		КонтрольнаяСумма = КонтрольнаяСумма + ЦифраИНН * Число(Множители[Сч - 1]);
	КонецЦикла;
	
	Возврат КонтрольнаяСумма;
	
КонецФункции

#КонецОбласти


